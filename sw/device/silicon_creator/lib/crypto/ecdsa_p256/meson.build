# Copyright lowRISC contributors.
# Licensed under the Apache License, Version 2.0, see LICENSE for details.
# SPDX-License-Identifier: Apache-2.0

# Create otbn sources dictionary
sw_lib_crypto_otbn_sources = {
  'p256': files(
    'p256.s'
  ),
  'p256_ecdsa': files(
    'p256_ecdsa.s',
    'p256.s',
  ),
}

# Setup for building OTBN assembly. OTBN software is build with a separate
# toolchain, which is called as an external target from Meson in
# subdirectories.

prog_otbn_build = meson.source_root() / 'util/otbn_build.py'

otbn_build_command = [
  prog_env,
  'OTBN_AS=@0@'.format(prog_otbn_as),
  'OTBN_LD=@0@'.format(prog_otbn_ld),
  'RV32_TOOL_OBJCOPY=@0@'.format(prog_objcopy.path()),
  'RV32_TOOL_AS=@0@'.format(prog_as.path()),
  'RV32_TOOL_LD=@0@'.format(prog_ld.path()),
  prog_python,
  prog_otbn_build,
  '--out-dir',
  '@OUTDIR@',
  '@INPUT@',
]
otbn_build_depend_files = [
  prog_otbn_as,
  prog_otbn_ld,
  prog_objcopy.path(),
  prog_as.path(),
  prog_ld.path(),
  prog_otbn_build,
]

# OTBN BUILD procedure
sw_lib_crypto_otbn = {}
foreach sw_lib_crypto_otbn__app_name, sw_lib_crypto_otbn__app_sources : sw_lib_crypto_otbn_sources
  # Output files generated by the otbn_build.py script.
  sw_lib_crypto_otbn__app_output_files = [
    sw_lib_crypto_otbn__app_name + '.rv32embed.o',
    sw_lib_crypto_otbn__app_name + '.elf',
  ]

  # Target calling otbn_build.py
  sw_lib_crypto_otbn__target = custom_target(
    'sw_lib_crypto_otbn_apps_' + sw_lib_crypto_otbn__app_name + '_target',
    input: sw_lib_crypto_otbn__app_sources,
    output: sw_lib_crypto_otbn__app_output_files,
    command: otbn_build_command,
    depend_files: [otbn_build_depend_files,],
  )

  # A library containing the OTBN application in a form embeddable into device
  # (Ibex) software (the *.rv32embed.o file).
  sw_lib_crypto_otbn__embedded_lib = static_library(
    sw_lib_crypto_otbn__app_name,
    [sw_lib_crypto_otbn__target[0]] # == sw_lib_crypto_otbn__app_output_files[0], i.e. *.rv32embed.o
  )

  # A dependency on the application as embeddable library, to be used if
  # device (Ibex) software wants to include an OTBN application in its binary.
  sw_lib_crypto_otbn__dependency = declare_dependency(
    link_with: sw_lib_crypto_otbn__embedded_lib,
  )

  sw_lib_crypto_otbn += {
    sw_lib_crypto_otbn__app_name: {
      'elf': sw_lib_crypto_otbn__target[1],
      'rv32embed_lib': sw_lib_crypto_otbn__embedded_lib,
      'rv32embed_dependency': sw_lib_crypto_otbn__dependency,
    }
  }

  custom_target(
    'sw_lib_crypto_otbn_app_export_' + sw_lib_crypto_otbn__app_name,
    command: export_target_command,
    depend_files: [export_target_depend_files,],
    input: [sw_lib_crypto_otbn__target[1]],
    output: 'sw_lib_crypto_otbn_app_export_' + sw_lib_crypto_otbn__app_name,
    build_always_stale: true,
    build_by_default: true,
  )

endforeach

# C wrapper for ECDSA sign/verify
sw_silicon_creator_lib_crypto_ecdsa_p256 = declare_dependency(
  link_with: static_library(
    'sw_silicon_creator_lib_crypto_ecdsa_p256',
    sources: [
      'ecdsa_p256.c',
    ],
    dependencies: [
      sw_silicon_creator_lib_otbn_util,
      sw_lib_crypto_otbn['p256_ecdsa']['rv32embed_dependency'],
    ],
  ),
)
