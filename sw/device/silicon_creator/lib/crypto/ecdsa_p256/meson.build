# Copyright lowRISC contributors.
# Licensed under the Apache License, Version 2.0, see LICENSE for details.
# SPDX-License-Identifier: Apache-2.0

# Create otbn sources dictionary
sw_lib_crypto_ecdsa_p256_otbn_sources = {
  'p256': files(
    'p256.s'
  ),
  'p256_ecdsa': files(
    'p256_ecdsa.s',
    'p256.s',
  ),
}

# OTBN BUILD procedure
sw_lib_crypto_ecdsa_p256_otbn = {}
foreach app_name, app_sources : sw_lib_crypto_ecdsa_p256_otbn_sources
  # Output files generated by the otbn_build.py script.
  app_output_files = [
    app_name + '.rv32embed.o',
    app_name + '.elf',
  ]

  # Target calling otbn_build.py
  target = custom_target(
    'sw_lib_crypto_otbn_apps_' + app_name + '_target',
    input: app_sources,
    output: app_output_files,
    command: otbn_build_command,
    depend_files: [otbn_build_depend_files,],
  )

  # A library containing the OTBN application in a form embeddable into device
  # (Ibex) software (the *.rv32embed.o file).
  embedded_lib = static_library(
    app_name,
    [target[0]] # == app_output_files[0], i.e. *.rv32embed.o
  )

  # A dependency on the application as embeddable library, to be used if
  # device (Ibex) software wants to include an OTBN application in its binary.
  app_dependency = declare_dependency(
    link_with: embedded_lib,
  )

  sw_lib_crypto_ecdsa_p256_otbn += {
    app_name: {
      'elf': target[1],
      'rv32embed_lib': embedded_lib,
      'rv32embed_dependency': app_dependency,
    }
  }

  custom_target(
    'sw_lib_crypto_ecdsa_p256_otbn_app_export_' + app_name,
    command: export_target_command,
    depend_files: [export_target_depend_files,],
    input: [target[1]],
    output: 'sw_lib_crypto_ecdsa_p256_otbn_app_export_' + app_name,
    build_always_stale: true,
    build_by_default: true,
  )

endforeach

# C wrapper for ECDSA sign/verify
sw_silicon_creator_lib_crypto_ecdsa_p256 = declare_dependency(
  link_with: static_library(
    'sw_silicon_creator_lib_crypto_ecdsa_p256',
    sources: [
      'ecdsa_p256.c',
    ],
    dependencies: [
      sw_silicon_creator_lib_otbn_util,
      sw_lib_crypto_ecdsa_p256_otbn['p256_ecdsa']['rv32embed_dependency'],
    ],
  ),
)
